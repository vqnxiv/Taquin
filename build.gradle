plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id("de.jjohannes.extra-java-module-info") version "0.6"
}

group 'io.github.vqnxiv'
version '0.1'

repositories {
    mavenCentral()
}

sourceCompatibility = "17.PREVIEW"
targetCompatibility = "17.PREVIEW"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

application {
    mainModule = 'io.github.vqnxiv.taquin'
    mainClass = 'io.github.vqnxiv.taquin.Taquin'
}

javafx {
    version = '17-ea+11'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.2')

    implementation 'org.openjdk.jol:jol-core:0.16'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.jfxtras:jmetro:11.6.15'
    implementation 'org.apache.logging.log4j:log4j-api:2.16.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.16.0'
}

extraJavaModuleInfo {
    //automaticModule("jol-core-0.16.jar", "")
    automaticModule("reflections-0.10.2.jar", "")
    

    // keep if JOL and reflections
    automaticModule("javassist-3.28.0-GA.jar", "javassist")
    automaticModule("jsr305-3.0.2.jar", "jsr305")
    
    
    module("jol-core-0.16.jar", "org.openjdk.jol", "0.16") {
        requires("jdk.unsupported")
        requires("java.management")
        exports("org.openjdk.jol.info")
    }
    /*
    module("reflections-0.10.2.jar", "org.reflections", "0.10.2") {
        exports("org.reflections")
        exports("org.reflections.scanners")
    }
    */
}

test {
    useJUnitPlatform()
}